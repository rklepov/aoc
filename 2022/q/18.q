// 2022/q/18.q
//
// https://adventofcode.com/2022/day/18

inp:("jjj";",")0:`:./input/18.txt;

// part 1
-1"";

grid:(2+1+max flip inp)#0N; / one extra layer on each side of the 3D grid
cube:flip inp+1;

neighbor:{x+/:\:raze(rotate[1]\)each -1 1,\:0 0};

cool:{[g;c;n]
  n@:where not null g ./:n;
  g:.[g;c;:;6-count n];
  ./[g;n;-;1]
 };

obsidian:cool/[grid;cube;neighbor cube];

show r:3 sum/0^obsidian; / 4282

// part 2
-1"";

dim:count each -1_first scan;
fr:flip reverse@;
rf:reverse flip@;

scanRow:{
  g:group s:sums differ null x;
  @[count[x]#0;-1_1_g distinct s where null x;:;1]
 };

scanSlice:{[rot;tor;g;nd]
  (tor scanRow each rot g .@[count[dim g]#(::);nd;:;]::)@/:til dim[g]nd
 };

/ Translates plain array index to the indices of a multi-dimensional array.
translateXYZ:{[ds;ix]
  {(y(div/)'reverse -1_'(,)\[reverse x])mod x}[ds;ix]
 };

findHollows:{[droplet]
  xscan:scanSlice[::;::;droplet;0]; / scan Y0Z slices in Z (horizontal) direction
  yscan:scanSlice[fr;rf;droplet;1]; / scan X0Z slices in X (vertical) direction, need to rotate 90dg
  zscan:scanSlice[::;::;droplet;2]; / scan X0Y slices in Y (horizontal) direction
  marker:3=sum(::;flip;(flip')flip::)@'(xscan;yscan;zscan);
  translateXYZ[dim droplet]each where(raze/)marker
 };

/ With this kind of scan we can mark some extra "pockets" which are actually
/ accessible by water via a series of turns (like spirals or so) and therefore
/ were falsely detected by a direct row scan through each axis.  Such cases will
/ be corrected on a later stage: the respective positions will have exposed
/ surface area > 0 so we'll be able to find them and unwind recursively.
//
/ The example of such  case is illustrated at the bottom of this file, the
/ position in the original hollow droplet is [7;20;13] (marked with *).
hollow:findHollows[obsidian];

solidDroplet:cool/[obsidian;hollow;neighbor hollow];

unwind:{[g;d;n]
  n@:where not null g ./:n;
  g:.[g;d;:;0N];
  ./[g;n;+;1]
 };

correct:{[hollow;solid]
  hollow@:where 0<solid ./:hollow;
  unwind/[solid;hollow;neighbor hollow]
 };

solidDroplet:correct[hollow]/[solidDroplet];

show r:3 sum/0^solidDroplet; / 2452

// The facilities to display the slices of the 3D grid

frame:{[w;v;m]
  c,/:(r,m,r:w#enlist dim[m][1]#v),\:c:w#v
 };

unframe:{[w;m]
  drop:w _neg[w]_;
  (drop')drop m
 };

displaySlice:{[m]
    frame[1;"+"],\:[;" "](raze'){(0N,s)!enlist[" ."]," ",/:string s:til 7}[]unframe[1;m]
 };

// [7;20;13]
//
/ displaySlice[obsidian[7;;]]                                     displaySlice[solidDroplet[7;;]]
/ X=7                      1                   2                                           1                   2
/ Y   Z  1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3                   1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3
/ 0   "+ + + + + + + + + + + + + * + + + + + + + + + +"           0   "+ + + + + + + + + + + + + + + + + + + + + + + +"
/ 1   "+ . . . . . . . . . . . . . . . . . . . . . . +"           1   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 2   "+ . . . . . . . . . . . . . . . . . . . . . . +"           2   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 3   "+ . . . . . . . 3 1 2 1 2 2 3 . . . . . . . . +"           3   "+ . . . . . . . 3 1 2 1 2 2 3 . . . . . . . . +"
/ 4   "+ . . . . . . 3 0 0 0 0 1 1 1 1 3 . . . . . . +"           4   "+ . . . . . . 3 0 0 0 0 0 1 0 1 3 . . . . . . +"
/ 5   "+ . . . . . 2 0 0 0 1 2 . 2 . 1 0 2 2 5 . . . +"           5   "+ . . . . . 2 0 0 0 0 0 0 0 0 0 0 2 2 5 . . . +"
/ 6   "+ . . . . 2 0 0 0 1 . 3 . 3 . 2 0 0 1 . . . . +"           6   "+ . . . . 2 0 0 0 0 0 0 0 0 0 0 0 0 1 . . . . +"
/ 7   "+ . . . . 2 0 0 0 2 . . 4 1 . . 2 1 0 2 . . . +"           7   "+ . . . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . . +"
/ 8   "+ . . . 2 0 1 0 2 3 . . . 5 . 3 0 0 0 1 . . . +"           8   "+ . . . 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . . . +"
/ 9   "+ . . 4 0 1 . 2 . . . . . . . 3 1 0 0 0 2 . . +"           9   "+ . . 4 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . +"
/ 10  "+ . . . 1 1 3 3 . . . . . . . . 4 1 0 0 2 . . +"           10  "+ . . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . +"
/ 11  "+ . . 3 0 0 2 . . . . . . . . . . 1 0 0 0 3 . +"           11  "+ . . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 3 . +"
/ 12  "+ . . 1 0 0 3 . . . . . . . 6 . . 1 0 0 1 3 . +"           12  "+ . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 3 . +"
/ 13  "+ . . 2 0 0 2 . . . . . . . . . . 1 0 0 2 . . +"           13  "+ . . 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . +"
/ 14  "+ . . 3 0 0 0 4 . . . . . . . . 4 1 0 0 2 . . +"           14  "+ . . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . +"
/ 15  "+ . . . 1 0 1 . 4 . . . 6 . . . 2 0 0 1 . . . +"           15  "+ . . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . . . +"
/ 16  "+ . . . 3 0 0 2 . 5 . . . . 3 3 0 0 0 2 4 . . +"           16  "+ . . . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 4 . . +"
/ 17  "+ . . . . 3 0 0 2 . . 3 1 2 1 . 1 0 3 . . . . +"           17  "+ . . . . 3 0 0 0 0 0 0 0 0 0 0 0 0 3 . . . . +"
/ 18  "+ . . . . . 2 0 0 1 2 0 2 . 1 1 1 3 . . . . . +"           18  "+ . . . . . 2 0 0 0 0 0 0 0 0 0 1 3 . . . . . +"
/ 19  "+ . . . . . 4 2 2 1 0 0 0 2 2 1 4 . . . . . . +"           19  "+ . . . . . 4 2 2 1 0 0 0 1 2 1 3 . . . . . . +"
/ 20  "* . . . . . . . . . 3 1 3 * . 5 . . . . . . . +"           20  "+ . . . . . . . . . 3 1 3 . . 5 . . . . . . . +"
/ 21  "+ . . . . . . . . . . . . 5 . . . . . . . . . +"           21  "+ . . . . . . . . . . . . 5 . . . . . . . . . +"
/ 22  "+ . . . . . . . . . . . . . . . . . . . . . . +"           22  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 23  "+ + + + + + + + + + + + + + + + + + + + + + + +"           23  "+ + + + + + + + + + + + + + + + + + + + + + + +"
//
/ displaySlice[obsidian[;20;]]                                    displaySlice[solidDroplet[;20;]]
/ Y=20                     1                   2                                           1                   2
/ X   Z  1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3                   1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3
/ 0   "+ + + + + + + + + + + + + * + + + + + + + + + +"           0   "+ + + + + + + + + + + + + + + + + + + + + + + +"
/ 1   "+ . . . . . . . . . . . . . . . . . . . . . . +"           1   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 2   "+ . . . . . . . . . . . . . . . . . . . . . . +"           2   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 3   "+ . . . . . . . . . . . . . . . . . . . . . . +"           3   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 4   "+ . . . . . . . . . . . . . . . . . . . . . . +"           4   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 5   "+ . . . . . . . . . . . . . . . . . . . . . . +"           5   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 6   "+ . . . . . . . . . . 4 . 4 5 . . . . . . . . +"           6   "+ . . . . . . . . . . 4 . 4 5 . . . . . . . . +"
/ 7   "* . . . . . . . . . 3 1 3 * . 5 . . . . . . . +"           7   "+ . . . . . . . . . 3 1 3 . . 5 . . . . . . . +"
/ 8   "+ . . . . . . . . 3 0 1 0 1 3 . 5 . . . . . . +"           8   "+ . . . . . . . . 3 0 1 0 1 3 . 4 . . . . . . +"
/ 9   "+ . . . . . . 3 2 1 0 1 0 1 1 2 2 . . . . . . +"           9   "+ . . . . . . 3 2 1 0 1 0 1 1 2 2 . . . . . . +"
/ 10  "+ . . . . . . 2 1 0 0 0 0 0 0 1 2 . . . . . . +"           10  "+ . . . . . . 2 1 0 0 0 0 0 0 1 2 . . . . . . +"
/ 11  "+ . . . . . 4 2 0 0 0 0 0 1 1 1 2 . . . . . . +"           11  "+ . . . . . 4 2 0 0 0 0 0 1 1 1 2 . . . . . . +"
/ 12  "+ . . . . . . . 2 1 0 1 0 1 1 0 1 4 . . . . . +"           12  "+ . . . . . . . 2 1 0 0 0 0 0 0 1 4 . . . . . +"
/ 13  "+ . . . . . . 4 1 1 0 0 1 . . 1 2 . . . . . . +"           13  "+ . . . . . . 4 1 1 0 0 0 0 0 0 2 . . . . . . +"
/ 14  "+ . . . . . . . 2 1 1 0 1 1 1 1 2 5 . . . . . +"           14  "+ . . . . . . . 2 1 1 0 1 0 0 1 2 5 . . . . . +"
/ 15  "+ . . . . . . . 2 2 0 0 1 1 2 2 . . . . . . . +"           15  "+ . . . . . . . 2 2 0 0 1 1 2 2 . . . . . . . +"
/ 16  "+ . . . . . . . 4 . 3 2 2 2 . 4 . . . . . . . +"           16  "+ . . . . . . . 4 . 3 2 2 2 . 4 . . . . . . . +"
/ 17  "+ . . . . . . . . . . . . 4 . . . . . . . . . +"           17  "+ . . . . . . . . . . . . 4 . . . . . . . . . +"
/ 18  "+ . . . . . . . . . . . . . . . . . . . . . . +"           18  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 19  "+ . . . . . . . . . . . . . . . . . . . . . . +"           19  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 20  "+ . . . . . . . . . . . . . . . . . . . . . . +"           20  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 21  "+ . . . . . . . . . . . . . . . . . . . . . . +"           21  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 22  "+ . . . . . . . . . . . . . . . . . . . . . . +"           22  "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 23  "+ + + + + + + + + + + + + + + + + + + + + + + +"           23  "+ + + + + + + + + + + + + + + + + + + + + + + +"
//
/ displaySlice[obsidian[;;13]]                                    displaySlice[solidDroplet[;;13]]
/ Z=13                     1                   2                                           1                   2
/ X   Y  1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3                   1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3
/ 0   "+ + + + + + + + + + + + + + + + + + + + * + + +"           0   "+ + + + + + + + + + + + + + + + + + + + + + + +"
/ 1   "+ . . . . . . . . . . . . . . . . . . . . . . +"           1   "+ . . . . . . . . . . . . . . . . . . . . . . +"
/ 2   "+ . . . . . . 5 2 2 . 3 1 3 . . . . . . . . . +"           2   "+ . . . . . . 5 2 2 . 3 1 3 . . . . . . . . . +"
/ 3   "+ . . . . . 3 . 1 1 1 0 0 0 1 1 1 3 . . . . . +"           3   "+ . . . . . 3 . 1 0 1 0 0 0 1 1 1 3 . . . . . +"
/ 4   "+ . . . . 4 0 1 1 . 1 2 1 1 0 0 0 1 . . . . . +"           4   "+ . . . . 4 0 1 0 0 0 0 0 0 0 0 0 1 . . . . . +"
/ 5   "+ . . . . 2 0 0 2 . 2 3 4 . 3 0 0 0 2 . . . . +"           5   "+ . . . . 2 0 0 0 0 0 0 0 0 0 0 0 0 2 . . . . +"
/ 6   "+ . . . . 1 0 1 . . 5 . . 6 . 3 2 0 1 2 4 . . +"           6   "+ . . . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 2 4 . . +"
/ 7   "* . . 2 1 2 3 1 5 . . . . . . . . 2 . 2 * 5 . +"           7   "+ . . 2 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . 5 . +"
/ 8   "+ . . 1 0 1 . 5 . . . . . . . . . 3 . 1 1 3 . +"           8   "+ . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 3 . +"
/ 9   "+ . . 1 0 3 . . . . . . . . . . . . 4 0 1 . . +"           9   "+ . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . . +"
/ 10  "+ . 3 0 0 1 . . . . . . . . . . . . . 1 0 3 . +"           10  "+ . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 3 . +"
/ 11  "+ . 2 1 1 4 . . . . . . . . . . . . . 1 1 . . +"           11  "+ . 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . . +"
/ 12  "+ . . 2 . . . . . . . . . . . . . . . 1 1 2 . +"           12  "+ . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . +"
/ 13  "+ . 3 0 1 2 . . . . . . . . . . . . . 2 . 2 . +"           13  "+ . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . +"
/ 14  "+ . 3 0 0 2 . . . . . . . . . . . . . 1 1 3 . +"           14  "+ . 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 3 . +"
/ 15  "+ . . 2 0 1 . . . . . . . . . . . 4 1 0 1 . . +"           15  "+ . . 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 . . +"
/ 16  "+ . . . 1 0 2 . . . . . . . . 5 . . 1 0 2 . . +"           16  "+ . . . 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 . . +"
/ 17  "+ . . 5 1 0 0 3 . . . 6 . . . 3 2 1 0 1 4 . . +"           17  "+ . . 5 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 4 . . +"
/ 18  "+ . . . . 2 2 . 2 2 . . 3 1 1 0 0 1 2 . . . . +"           18  "+ . . . . 2 1 0 0 0 0 0 0 0 0 0 0 0 2 . . . . +"
/ 19  "+ . . . . . . 2 0 0 1 1 0 1 0 0 0 2 . . . . . +"           19  "+ . . . . . . 1 0 0 0 0 0 0 0 0 0 2 . . . . . +"
/ 20  "+ . . . . . 5 2 2 0 0 0 0 1 0 0 2 . . . . . . +"           20  "+ . . . . . 5 2 2 0 0 0 0 0 0 0 2 . . . . . . +"
/ 21  "+ . . . . . . . . 2 1 1 1 1 1 4 . . . . . . . +"           21  "+ . . . . . . . . 2 1 1 1 1 1 4 . . . . . . . +"
/ 22  "+ . . . . . . . . . . . 5 . . . . . . . . . . +"           22  "+ . . . . . . . . . . . 5 . . . . . . . . . . +"
/ 23  "+ + + + + + + + + + + + + + + + + + + + + + + +"           23  "+ + + + + + + + + + + + + + + + + + + + + + + +"

exit 0;

// __EOF__
