// 2022/q/14.q
//
// https://adventofcode.com/2022/day/14

inp:read0`:./input/14.txt;
-1"";

// utilities
dim:count each -1_first scan;

split:{[pred;v]
  g:v group s:sums differ w:pred v;
  g distinct s where not w
 };

byDelim:{[d;s]@[count[s]#0b;;:;1b]raze w where d~/:s w:count[s](1+)\til count d};

frame:{[w;v;m]
  c,/:(r,m,r:w#enlist dim[m][1]#v),\:c:w#v
 };

unframe:{[w;m]
  drop:w _neg[w]_;
  (drop')drop m
 };

// map legend, global
D:{x!til count x}".+o#";

// matrix indices are the reversed (X;Y) coordinates
rockShape:"J"$((reverse split[","=]::)each split[byDelim" -> "]@)each inp;

buildMap:{[sandSrc;rockShape]
  caveRect:flip((min;max)@\:raze rockShape .(::;::;)::)each 0 1;
  caveRect[0;0]:0;
  cave:(1+(-/)reverse caveRect)#D["."];
  rockShape:rockShape-\:\:caveRect 0;
  rocks:{[rs]
    distinct raze first[rs]{[b;a]
      flip a+({signum[x]*til 1+abs x};#[1+sum abs d])[0=d]@'d:b-a
    }':rs
  }each rockShape;
  `cave`sandSrc!(./[cave;raze[rocks];:;D["#"]];sandSrc-caveRect 0)
 };

caveMap:buildMap[(0;500);rockShape];
/ D?caveMap`cave

fall:{
  / Trying all 3 options and taking the first one with a change is probably not
  / optimal. But the alternatives are ugly (like a while loop with a counter).
  r:first r where 1_differ enlist[y],
    r:y^/:(::;next;prev)@'
      {@[y;where[D["."]=y]inter where D["+"]=x;1+]}[x]each
    D["."]^(::;prev;next)@\:y;
  / Actually no need to return the unchanged row, but it's useful to trace and
  / display the path of a grain of sand.
  if[0=count r;r:y];
  r
 };

// part 1
simAbyss:{[sandSrc;cave]
  m:frame[1;D["."];cave]; / frame models the abyss
  m:.[m;@[sandSrc;1;+;1];:;D["+"]]; / adjust sand source location

  m:fall scan m;
  m:.[m;(ix;iy ix:last where count'[m]>iy:m?'D["+"]);:;D["o"]]; / come to rest
  m:@'[m;(where')D["+"]=m;:;D["."]]; / replace the path with air

  unframe[1;m] / will disappear in the void if flows out the bottom
 };

sand:simAbyss[caveMap`sandSrc]over caveMap`cave;
system "c "," "sv string 3+dim sand;
show D?sand;
-1"";

show 2 sum/D["o"]=sand; / 1072

/      4         4         4         4         5         5         5         5
/      6         7         8         9         0         1         2         3
/      012345678901234567890123456789012345678901234567890123456789012345678901234567
//
/                1         2         3         4         5         6         7
/      012345678901234567890123456789012345678901234567890123456789012345678901234567
/ 0   ".............................................................................."
/ 1   ".............................................................................."
/ 2   ".............................................................................."
/ 3   ".............................................................................."
/ 4   ".............................................................................."
/ 5   ".............................................................................."
/ 6   ".............................................................................."
/ 7   ".............................................................................."
/ 8   "........................................o....................................."
/ 9   ".......................................ooo...................................."
/ 10  "......................................ooooo..................................."
/ 11  ".....................................ooooooo.................................."
/ 12  "....................................ooooooooo................................."
/ 13  "...................................ooooooooooo................................"
/ 14  "..................................ooooooooooooo..............................."
/ 15  ".................................ooo#ooooooooooo.............................."
/ 16  "................................oooo###############..........................."
/ 17  "...............................oooooo........................................."
/ 18  "..............................oooooooo........................................"
/ 19  ".............................oooo#ooooo......................................."
/ 20  "............................ooooo#oooooo......................................"
/ 21  "...........................#ooooo#ooooooo....................................."
/ 22  "...........................#o#o#o#oooooooo...................................."
/ 23  "...........................#o#o#o#o#ooo#ooo..................................."
/ 24  "...........................#o#o#o#o#ooo#oooo.................................."
/ 25  "...........................#o#o#o#o#ooo#ooooo................................."
/ 26  "...........................#o#o#o#o#ooo#o#oooo................................"
/ 27  "...........................#o#o#o#o#o#o#o#ooooo..............................."
/ 28  "...........................#o#o#o#o#o#o#o#oooooo.............................."
/ 29  "...........................###############ooooooo............................."
/ 30  ".........................................ooooooooo............................"
/ 31  "........................................ooooooooooo..........................."
/ 32  ".......................................#ooooo#oooooo.........................."
/ 33  ".......................................#ooooo#ooooooo........................."
/ 34  ".......................................#ooooo#oooooooo........................"
/ 35  "......................................##ooooo########oo......................."
/ 36  "......................................#ooooooo......#ooo......................"
/ 37  "......................................#oooooooo.....#oooo....................."
/ 38  "......................................#ooooooooo....#ooooo...................."
/ 39  "......................................#oooooooooo...#oooooo..................."
/ 40  "......................................###############ooooooo.................."
/ 41  "....................................................ooooooooo................."
/ 42  "...................................................ooooooooooo................"
/ 43  "..................................................######ooooooo..............."
/ 44  ".......................................................ooooooooo.............."
/ 45  "...............................................######.######ooooo............."
/ 46  "...........................................................ooooooo............"
/ 47  "............................................######.######.######ooo..........."
/ 48  "...............................................................ooooo.........."
/ 49  ".........................................######.######.######.######o........."
/ 50  "...................................................................ooo........"
/ 51  "..................................................................ooooo......."
/ 52  ".................................................................ooooooo......"
/ 53  "................................................................ooooooooo....#"
/ 54  "...............................................................oo#############"
/ 55  "..............................................................oooo............"
/ 56  ".............................................................oooooo..........."
/ 57  "............................................................oo#ooo#o.........."
/ 58  "...........................................................ooo#ooo#oo........."
/ 59  "........................................................#######ooo######......"
/ 60  "........................................................#.....ooooo....#......"
/ 61  "........................................................#....ooooooo...#......"
/ 62  "........................................................#...ooooooooo..#......"
/ 63  "........................................................#..ooooooooooo.#......"
/ 64  "........................................................#.ooooooooooooo#......"
/ 65  "........................................................#oooooooooooooo#......"
/ 66  "........................................................#oooooooooooooo#......"
/ 67  "........................................................################......"
/ 68  ".........................oo..................................................."
/ 69  "........................oooo.................................................."
/ 70  ".......................oooooo................................................."
/ 71  "......................ooo#ooo#................................................"
/ 72  ".....................oooo#ooo#................................................"
/ 73  "....................ooooo#ooo#................................................"
/ 74  "...................oooooo#o#o#o..............................................."
/ 75  "..................ooooooo#o#o#o#.............................................."
/ 76  ".................oooooooo#o#o#o#.............................................."
/ 77  "................ooooo#o#o#o#o#o#.............................................."
/ 78  "...............oooo#o#o#o#o#o#o#.............................................."
/ 79  "..............ooooo#o#o#o#o#o#o#.............................................."
/ 80  ".............oooooo#############.............................................."
/ 81  "............oooooooo.........................................................."
/ 82  "...........oooooooooo........................................................."
/ 83  "..........oooooooooooo........................................................"
/ 84  ".........ooo###########......................................................."
/ 85  "........ooooo................................................................."
/ 86  ".......ooooooo................................................................"
/ 87  "......ooo#####o..............................................................."
/ 88  ".....ooooo...ooo.............................................................."
/ 89  "....oo#####.#####............................................................."
/ 90  "...oooo......................................................................."
/ 91  "..o#####.#####.#####............o............................................."
/ 92  ".ooo...........................ooo............................................"
/ 93  "#####.#####.#####.#####.......ooooo..........................................."
/ 94  ".............................ooooooo.........................................."
/ 95  "............................ooooooooo........................................."
/ 96  "...........................ooo#####ooo........................................"
/ 97  "..........................ooooo...ooooo......................................."
/ 98  ".........................oo#####.#####oo......................................"
/ 99  "........................oooo.........oooo....................................."
/ 100 ".......................o#####.#####.#####o...................................."
/ 101 "......................ooo...............ooo..................................."
/ 102 ".....................#####.#####.#####.#####.................................."
/ 103 "...................oo........................................................."
/ 104 "..................oooo........................................................"
/ 105 ".................oo#oo#......................................................."
/ 106 "................ooo#oo#......................................................."
/ 107 "............########oo##......................................................"
/ 108 "............#......oooo#......................................................"
/ 109 "............#.....ooooo#......................................................"
/ 110 "............#....oooooo#......................................................"
/ 111 "............#...ooooooo#......................................................"
/ 112 "............#..oooooooo#......................................................"
/ 113 "............############......................................................"
/ 114 ".............................................................................."
/ 115 ".............................................................................."
/ 116 ".............................................................................."
/ 117 ".............................................................................."
/ 118 "........................o....................................................."
/ 119 ".......................ooo...................................................."
/ 120 "......................ooooo..................................................."
/ 121 ".....................#oooooo.................................................."
/ 122 ".....................#o#ooooo................................................."
/ 123 ".....................#o#oooooo................................................"
/ 124 ".....................#o#ooooooo..............................................."
/ 125 ".....................#o#o#oooooo.............................................."
/ 126 ".....................#####ooooooo............................................."
/ 127 ".........................ooooooooo............................................"
/ 128 "........................ooooooooooo..........................................."
/ 129 ".......................ooooooooooooo.........................................."
/ 130 ".....................#ooooooooooooooo........................................."
/ 131 ".....................##############ooo........................................"
/ 132 "..................................ooooo......................................."
/ 133 "....................o............ooooooo......................................"
/ 134 "...................ooo..........######ooo....................................."
/ 135 "..................ooooo..............ooooo...................................."
/ 136 ".................ooooooo.....######.######o..................................."
/ 137 "................ooooooooo................ooo.................................."
/ 138 "...............ooooooooooo######.######.######................................"
/ 139 "..............ooooooooooooo..................................................."
/ 140 ".............ooooooooooooooo.................................................."
/ 141 "............ooooo#ooooo#ooooo................................................."
/ 142 "...........oooooo#ooooo#oooooo................................................"
/ 143 "..........ooooooo#ooooo#ooooooo..............................................."
/ 144 ".........#########ooooo#########.............................................."
/ 145 ".........#.......ooooooo.......#.............................................."
/ 146 ".........#......ooooooooo......#.............................................."
/ 147 ".........#.....ooooooooooo.....#.............................................."
/ 148 ".........#######################.............................................."
/ 149 ".............................................................................."
/ 150 "........o....................................................................."
/ 151 ".......#o#...................................................................."
/ 152 ".......#o#...................................................................."
/ 153 ".......#o#...................................................................."
/ 154 "......o#o#...................................................................."
/ 155 ".....#o#o#...................................................................."
/ 156 ".....#o#o#...................................................................."
/ 157 ".....#o#o#...................................................................."
/ 158 ".....#o#o#...................................................................."
/ 159 ".....#o#o#...................................................................."
/ 160 ".....#o#o#...................................................................."
/ 161 ".....#####...................................................................."

exit 0;

// __EOF__
